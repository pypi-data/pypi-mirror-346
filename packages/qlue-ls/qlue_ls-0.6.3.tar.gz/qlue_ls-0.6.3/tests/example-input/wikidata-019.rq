#title: Total population in the Øresund Region
# Illustrates the SUM aggregate function

SELECT DISTINCT ?area ?areaLabel (sum(?folkm_ngd) as ?total_folkm) # the first two variables can be removed
                                                                   # if only the number is needed
WHERE {
  ?item wdt:P361 wd:Q297853. # part of (P361) Øresund Region (Q297853)
  ?item wdt:P1082 ?folkm_ngd. # population (P1082)
  ?area wdt:P31 wd:Q1907114. # instance of (P31) metropolitan area (Q1907114)
  ?area wdt:P527 ?item. # has part (P527)
  SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en". }
}
GROUP BY ?area ?areaLabel