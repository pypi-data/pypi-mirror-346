#title: Recent events
SELECT ?event ?eventLabel ?date WHERE {
  {
    SELECT DISTINCT ?event ?date WHERE {
      # find events
      ?event wdt:P31/wdt:P279* wd:Q1190554 .
      # with a point in time or start date
      OPTIONAL {
        ?event wdt:P585 ?date .
      }
      OPTIONAL {
        ?event wdt:P580 ?date .
      }
      # but at least one of those
      FILTER (BOUND(?date) && DATATYPE(?date) = xsd:dateTime) .
      # not in the future, and not more than 31 days ago
      
      BIND (NOW() - ?date AS ?distance) .
      
      FILTER (0 <= ?distance && ?distance < 31) .
    }
    LIMIT 150
  }
  SERVICE wikibase:label {
    bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en" .
  }
}
